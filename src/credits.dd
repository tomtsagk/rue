(include "credits.ddh")
(include "menu.ddh")

(class_function world_credits void create ()
	(group

	# background
	(this.bg.load (asset "assets/stage_2.ply" DD_PLY))

	(this.labelCredits.setText "Credits:")
	(this.labelCredits.setAlign DD_STRING3D_ALIGN_CENTER)

	(this.labelTom.setText "Tom Tsagk")
	(this.labelTom.setAlign DD_STRING3D_ALIGN_CENTER)

	(this.labelProgrammer.setText "Programmer")
	(this.labelProgrammer.setAlign DD_STRING3D_ALIGN_CENTER)

	(this.labelShiitakespacewarrior.setText "shiitakespacewarrior")
	(this.labelShiitakespacewarrior.setAlign DD_STRING3D_ALIGN_CENTER)

	(this.labelMusicSfx.setText "Music & SFX")
	(this.labelMusicSfx.setAlign DD_STRING3D_ALIGN_CENTER)

	(this.anchorCredits.setSize 1 1)
	(this.anchorCredits.setProportionalPosition 0.5 0.5)
	(this.anchorCredits.setProportionalWidth 0.05)
	(this.anchorCredits.setProportionalSize 1.0)

	(= this.animation 0)
	(= this.fastForward 0)

	)
)

(class_function world_credits void onload ()
	(group
	(dd_world_prepare world_menu 1.0)
	)
)

(class_function world_credits void resize ()
	(group
	(this.anchorCredits.resize)
	)
)

(class_function world_credits void update ()
	(group

	# animate credits
	(if this.fastForward
		(= this.animation (dd_math_min (+ this.animation 0.005) 1))
		(= this.animation (dd_math_min (+ this.animation 0.001) 1))
	)

	# credits finished - move back to main menu
	(if (>= this.animation 0.99)
		(group
		(dd_world_ready)
		)
	)

	)
) # update

(class_function world_credits void draw ()
	(group

	# draw background
	(dd_matrix_push)
	(dd_translatef 0 -3 -8)
	(dd_rotatef (* 180 this.animation) 0 1 0)
	(dd_rotatef 15 -1 0 0)
	(this.bg.draw)
	(dd_matrix_pop)

	(glClear GL_DEPTH_BUFFER_BIT)
	#(avdl_clear_depth)

	# draw the credits themselves
	(dd_matrix_push)
	(def float height)
	(= height (dd_screen_height_get (+ (* this.anchorCredits.z -1) 10)))
	(dd_translatef 0 (+ (* height -0.5) (* height this.animation 2)) -10)
	(this.anchorCredits.applyTransform)
	(dd_translatef 0 -1 1)
	(this.labelCredits.draw)
	(dd_translatef 0 -2.0 0)
	(this.labelTom.draw)
	(dd_translatef 0 -1 0)
	(this.labelProgrammer.draw)
	(dd_translatef 0 -2.0 0)
	(this.labelShiitakespacewarrior.draw)
	(dd_translatef 0 -1 0)
	(this.labelMusicSfx.draw)
	(dd_matrix_pop)

	)
)

(class_function world_credits void key_input (group char key)
	(group
	(if (== key 27)
		(dd_world_ready)
	) # key input
	)
)

(class_function world_credits void clean ()
	(group
	)
)

(class_function world_credits void mouse_input (group int button int type)
	(group
	(if (== type DD_INPUT_MOUSE_TYPE_PRESSED)
		(= this.fastForward 1)
	(== type DD_INPUT_MOUSE_TYPE_RELEASED)
		(= this.fastForward 0)
	)
	(return 0)
	)
) # mouse input
