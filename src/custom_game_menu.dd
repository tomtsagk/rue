(include "custom_game_menu.ddh")
(include "game.ddh")
(include "menu.ddh")

(def int numberOfPlayers)
(def int startingPlayer)
(def int guide)

(class_function world_custom_game_menu void create (group)
	(group

	# background
	(= this.bg_rotation 0)
	(this.bg.load (asset "assets/stage_2.asset" DD_PLY))

	# font
	(this.font.setAlign DD_STRING3D_ALIGN_CENTER)
	(this.font.setColorFront 1.0 1.0 1.0)
	#(this.font.setColorBack 1.0 0.0 0.0)
	(this.font.setColorBack 1.0 1.0 1.0)

	# buttons
	(= this.buttonTotal 7)
	(Button_create this.button[0])
	(this.button[0].mesh.load (asset "assets/button_minus.asset" DD_PLY))
	(this.button[0].setSize 1 1)
	(this.button[0].setProportionalPosition 0.35 0.75)
	(this.button[0].setProportionalWidth 0.2)
	(this.button[0].setProportionalSize 0.7)

	(Button_create this.button[1])
	(this.button[1].mesh.load (asset "assets/button_plus.asset" DD_PLY))
	(this.button[1].setSize 1 1)
	(this.button[1].setProportionalPosition 0.65 0.75)
	(this.button[1].setProportionalWidth 0.2)
	(this.button[1].setProportionalSize 0.7)

	(Button_create this.button[2])
	(this.button[2].mesh.load (asset "assets/button_minus.asset" DD_PLY))
	(this.button[2].setSize 1 1)
	(this.button[2].setProportionalPosition 0.35 0.5)
	(this.button[2].setProportionalWidth 0.2)
	(this.button[2].setProportionalSize 0.7)

	(Button_create this.button[3])
	(this.button[3].mesh.load (asset "assets/button_plus.asset" DD_PLY))
	(this.button[3].setSize 1 1)
	(this.button[3].setProportionalPosition 0.65 0.5)
	(this.button[3].setProportionalWidth 0.2)
	(this.button[3].setProportionalSize 0.7)

	(Button_create this.button[4])
	(this.button[4].mesh.load (asset "assets/button.asset" DD_PLY))
	(this.button[4].setSize 1 1)
	(this.button[4].setProportionalPosition 0.5 0.2)
	(this.button[4].setProportionalWidth 0.2)
	(this.button[4].setProportionalSize 0.7)

	(Button_create this.button[5])
	(this.button[5].mesh.load (asset "assets/button.asset" DD_PLY))
	(this.button[5].setSize 1 1)
	(this.button[5].setProportionalPosition 0.12 0.15)
	(this.button[5].setProportionalWidth 0.2)

	(Button_create this.button[6])
	(this.button[6].mesh.load (asset "assets/button.asset" DD_PLY))
	(this.button[6].setSize 1 1)
	(this.button[6].setProportionalPosition 0.88 0.15)
	(this.button[6].setProportionalWidth 0.2)

	(= this.selection -1)
	(= this.selectionClick -1)
	(= this.isMovingToNewWorld 0)

	(= numberOfPlayers 3)
	(= startingPlayer -1)
	(= guide 0)

	# shader for buttons
	(this.buttonProgram.setVertexShader
		(multistring
			"AVDL_IN vec4 position;\n"
			"AVDL_IN vec3 colour;\n"

			"uniform mat4 matrix;\n"
			"uniform vec3 tint;\n"

			"AVDL_OUT vec4 outColour;\n"

			"void main() {\n"
			"	vec4 pos = matrix *position;\n"
			"       gl_Position = pos;\n"
			"       outColour = vec4(colour.rgb *tint.rgb, 1.0);\n"
			"}\n"
		)
	)
	(this.buttonProgram.setFragmentShader
		(multistring
			"AVDL_IN vec4 outColour;\n"

			"void main() {\n"
			"       avdl_frag_color = outColour;\n"
			"}\n"
		)
	)

	# button colours
	(= this.buttonClickCol[0] 1.0)
	(= this.buttonClickCol[1] 0.0)
	(= this.buttonClickCol[2] 1.0)

	(= this.buttonActiveCol[0] 0.403)
	(= this.buttonActiveCol[1] 0.698)
	(= this.buttonActiveCol[2] 0.349)

	(= this.buttonInactiveCol[0] 0.694)
	(= this.buttonInactiveCol[1] 0.137)
	(= this.buttonInactiveCol[2] 0.137)

	)
)

(class_function world_custom_game_menu void onload (group)
	(group
	)
)

(class_function world_custom_game_menu void resize (group)
	(group
	(for (def int i 0) (< i this.buttonTotal) (= i (+ i 1))
		(this.button[i].resize)
	)
	)
)

(class_function world_custom_game_menu void update (group)
	(group

	# constant background rotation
	(= this.bg_rotation (+ this.bg_rotation 0.01))
	(if (>= this.bg_rotation 360)
		(= this.bg_rotation (- this.bg_rotation 360))
	)

	# check if mouse is hovering over a button
	(= this.selection -1)
	(for (def int i 0) (< i this.buttonTotal) (= i (+ i 1))
		(group

		(if (this.button[i].hasMouseCollided)
			(group
			(= this.selection i)
			)
		)

		(this.button[i].update (== this.selection i) (== this.selectionClick i))

		)
	)

	)
) # update

(class_function world_custom_game_menu void draw (group)
	(group

	# draw background
	(dd_matrix_push)
	(dd_translatef 0 -2 -3)
	(dd_rotatef (+ 40 this.bg_rotation) 0 1 0)
	(this.bg.draw)
	(dd_matrix_pop)

	# clear depth
	(glClear GL_DEPTH_BUFFER_BIT)

	# draw number of players
	(dd_matrix_push)
	(dd_translatef 0 1 -5)
	(dd_scalef 0.2 0.2 0.2)
	(this.font.drawInt numberOfPlayers)
	(dd_matrix_pop)

	# draw starting player
	(dd_matrix_push)
	(dd_translatef 0 0 -5)
	(dd_scalef 0.2 0.2 0.2)
	(if (== startingPlayer -1)
		(this.font.draw "random")
	(== startingPlayer 0)
		(this.font.draw "you")
		(group
		(dd_translatef -0.7 0 0)
		(this.font.draw "AI")
		(dd_translatef 1.4 0 0)
		(this.font.drawInt startingPlayer)
		)
	)
	(dd_matrix_pop)

	# draw buttons
	(this.buttonProgram.useProgram)
	(def int colLoc)
	(= colLoc (avdl_getUniformLocation this.buttonProgram "tint"))
	(for (def int i 0) (< i this.buttonTotal) (= i (+ i 1))
		(group
		(dd_matrix_push)

		# change the shader colour when clicked
		(if (> this.button[i].idleCol 1.0)
			(avdl_setUniform3f colLoc
				(dd_math_ease_linear
					(- this.button[i].idleCol 1.0)
					this.buttonActiveCol[0] this.buttonClickCol[0]
				)
				(dd_math_ease_linear
					(- this.button[i].idleCol 1.0)
					this.buttonActiveCol[1] this.buttonClickCol[1]
				)
				(dd_math_ease_linear
					(- this.button[i].idleCol 1.0)
					this.buttonActiveCol[2] this.buttonClickCol[2]
				)
			)
		# change colour when selected
			(avdl_setUniform3f colLoc
				(dd_math_ease_linear this.button[i].idleCol this.buttonInactiveCol[0] this.buttonActiveCol[0])
				(dd_math_ease_linear this.button[i].idleCol this.buttonInactiveCol[1] this.buttonActiveCol[1])
				(dd_math_ease_linear this.button[i].idleCol this.buttonInactiveCol[2] this.buttonActiveCol[2])
			)
		)

		(this.button[i].applyTransform)
		(this.button[i].applyMatrixTransform)
		(this.button[i].drawRaw)

		(dd_translatef 0 0 0.15)
		(dd_scalef 0.2 0.2 0.2)

		(if (== i 0)
			(group)
		(== i 1)
			(group)
		(== i 2)
			(group)
		(== i 3)
			(group)
		(== i 4)
			(if (== guide 0)
				(this.font.draw "include guide")
				(this.font.draw "no guide")
			)
		(== i 5)
			(this.font.draw "back")
		(== i 6)
			(this.font.draw "start")
		)

		(dd_matrix_pop)
		)
	)
	(avdl_useProgram 0)

	)
)

(class_function world_custom_game_menu void key_input (group char key)
	(group
	(if (== key 27)
		(= dd_flag_exit 1) # exit game
	(== key 97)
		(group
		(echo "start game")
		(dd_world_prepare world_game 1.0)
		(dd_world_ready)
		)
	) # key input
	)
)

(class_function world_custom_game_menu void clean (group)
	(group
	)
)

(class_function world_custom_game_menu void mouse_input (group int button int type)
	(group

	# control the camera look at point when click and dragging
	(if (== type DD_INPUT_MOUSE_TYPE_PRESSED)
		(group
		(if (>= this.selection 0)
			(= this.selectionClick this.selection)
		)
		)
	(== type DD_INPUT_MOUSE_TYPE_RELEASED)
		(group

		# clicked on a button
		(if (&& (== this.selection this.selectionClick) (== this.isMovingToNewWorld 0))
			(group

			# minus num of players
			(if (== this.selectionClick 0)
				(group
				(= numberOfPlayers (dd_math_max (- numberOfPlayers 1) 3))
				(= startingPlayer (dd_math_min startingPlayer (- numberOfPlayers 1)))
				)
			# plus num of players
			(== this.selectionClick 1)
				(group
				(= numberOfPlayers (dd_math_min (+ numberOfPlayers 1) 5))
				)
			# minus starting player
			(== this.selectionClick 2)
				(group
				(= startingPlayer (dd_math_max (- startingPlayer 1) -1))
				)
			# plus starting player
			(== this.selectionClick 3)
				(group
				(= startingPlayer (dd_math_min (+ startingPlayer 1) (- numberOfPlayers 1)))
				)
			# toggle guide
			(== this.selectionClick 4)
				(group
				(if (== guide 0)
					(= guide -1)
					(= guide 0)
				)
				)
			# back to main menu
			(== this.selectionClick 5)
				(group
				(dd_world_prepare world_menu 1.0)
				(dd_world_ready)
				(= this.isMovingToNewWorld 1)
				)
			# start game !
			(== this.selectionClick 6)
				(group
				(dd_world_prepare world_game 1.0)
				(dd_world_ready)
				(= this.isMovingToNewWorld 1)
				)
			)

			)
		)

		(= this.selectionClick -1)

		)
	) # mouse button released

	)
) # mouse input
