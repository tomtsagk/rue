(include "player.ddh")

(class_function Player void create (group)
	(group

	(= this.isUser 0)

	# player itself
	(this.mesh.load (asset "assets/character.asset" DD_PLY))
	(this.mesh.loadTexture (asset "assets/character_1.asset" DD_PLY))

	# individual cards
	(= this.cardsTotal 0)

	# font
	(this.font.setAlign DD_STRING3D_ALIGN_CENTER)
	(this.font.setColorFront 1.0 1.0 1.0)
	(this.font.setColorBack 0.0 0.0 0.0)

	# selection
	(= this.selectedCard -1)
	(= this.selectedCard2 0)
	(dd_matrix_identity this.selectedMatrix)
	(dd_matrix_translate this.selectedMatrix 0 2.01 3.5)
	(dd_matrix_rotate this.selectedMatrix 90 1 0 0)
	(dd_matrix_rotate this.selectedMatrix (- (dd_math_rand 20) 10) 0 0 1)
	(dd_matrix_scale this.selectedMatrix 0.15 0.15 0.15)

	)
)

(class_function Player void draw (group)
	(group

	(if this.isUser
		(this.drawUser)
		(this.drawAI)
	)

	# draw cards
	(for (def int i 0) (< i this.cardsTotal) (= i (+ i 1))
		(group
		(this.cards[i].draw)
		)
	)

	# draw selected card
	(if this.selectedCard2
		(this.selectedCard2.draw)
	)

	)
)

(class_function Player void drawUser (group)
	(group

	)
)

(class_function Player void drawAI (group)
	(group

	# draw player
	(dd_matrix_push)
	(dd_multMatrixf this)
	(this.mesh.draw)
	(dd_translatef 0 2.2 0.4)
	(dd_scalef 0.5 0.5 0.5)
	(this.font.drawInt this.cardsTotal)
	(dd_matrix_pop)

	)
)

(class_function Player void clean (group)
	(group
	)
)

# add new card to the hand
(def ref dd_meshColour newCardMesh)
(def ref Card newCard)

(class_function Player void addCard (group Card card)
	(group

	# apply limit
	(if (>= this.cardsTotal 10)
		(echo "CANNOT GIVE MORE CARDS")
	(group
	(= this.cards[this.cardsTotal] card)
	(= this.cardsTotal (+ this.cardsTotal 1))

	(if this.isUser
		(this.calculateCardPositionsUser)
		(this.calculateCardPositions)
	)
	)
	)

	)
)

(class_function Player void calculateCardPositions (group)
	(group
	(for (def int i 0) (< i this.cardsTotal) (= i (+ i 1))
		(group
		(dd_matrix_identity this.cards[i].target)
		(dd_matrix_mult this.cards[i].target this)
		(dd_matrix_translate this.cards[i].target 0 2.5 0.3)
		(dd_matrix_scale this.cards[i].target 0.15 0.15 0.15)
		(dd_matrix_translate this.cards[i].target (- (* i 2.7) (/ (* (- this.cardsTotal 1) 2.7) 2)) 0 0)
		(dd_matrix_rotate this.cards[i].target 180 0 1 0)
		)
	)
	)
)

(class_function Player void calculateCardPositionsUser (group)
	(group
	(for (def int i 0) (< i this.cardsTotal) (= i (+ i 1))
		(group
		(dd_matrix_identity this.cards[i].target)
		(dd_matrix_mult this.cards[i].target this)
		(dd_matrix_translate this.cards[i].target 0 3.7 0)
		(dd_matrix_rotate this.cards[i].target (- (* -25 i) (* (/ (- this.cardsTotal 1) 2.0) -25)) 0 1 0)
		(dd_matrix_rotate this.cards[i].target -35 1 0 0)
		(dd_matrix_translate this.cards[i].target 0 0 -1.2)
		(dd_matrix_rotate this.cards[i].target 15 1 0 0)
		(dd_matrix_scale this.cards[i].target 0.15 0.15 0.15)
		)
	)
	)
)

(class_function Player void selectCard (group)
	(group

	(if (<= this.cardsTotal 0)
		(group
		(echo "cannot select card, no cards left")
		)
		(group

		(def int index (dd_math_rand this.cardsTotal))
		(= this.selectedCard (dd_math_rand this.cardsTotal))
		(= this.selectedCard2 this.cards[index])
		(dd_matrix_identity this.selectedCard2.target)
		(dd_matrix_mult this.selectedCard2.target this)
		(dd_matrix_mult this.selectedCard2.target this.selectedMatrix)

		# remove card from array
		(for (def int i (+ index 1)) (< i this.cardsTotal) (= i (+ i 1))
			(group
			(def int prevIndex (- i 1))
			(= this.cards[prevIndex] this.cards[i])
			)
		)

		(= this.cardsTotal (- this.cardsTotal 1))
		(if this.isUser
			(this.calculateCardPositionsUser)
			(this.calculateCardPositions)
		)

		)
	)

	)
)

(class_function Player void deselectCard (group)
	(group
	(= this.selectedCard -1)
	)
)
