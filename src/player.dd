(include "player.ddh")

(def extern int CARD_ROSE)

(class_function Player void create ()
	(group

	(= this.isUser 0)

	# individual cards
	(= this.cardsTotal 0)

	# labels
	(this.label_name.setAlign DD_STRING3D_ALIGN_CENTER)
	(this.label_int.setAlign DD_STRING3D_ALIGN_CENTER)
	(this.label_int.setTextInt)
	#(this.font.setColorFront 0.9 0.9 0.9)
	#(this.font.setColorBack 0.3 0.3 0.3)

	# selection
	(= this.highlightCard -1)
	(= this.selectedCard -1)
	(= this.selectedCard2 0)
	(dd_matrix_identity this.selectedMatrix)
	(dd_matrix_translate this.selectedMatrix 0 2.01 2.6)
	(dd_matrix_rotate this.selectedMatrix 90 1 0 0)
	#(dd_matrix_rotate this.selectedMatrix (- (dd_math_rand 20) 10) 0 0 1)

	(= this.userLookingAtCard -1)

	(= this.cardFronts 0)

	(= this.labelConfirm 0)

	)
)

(class_function Player void draw ()
	(group

	(if this.isUser
		(this.drawUser)
		(this.drawAI)
	)

	# draw cards
	(for (def int i 0) (< i this.cardsTotal) (= i (+ i 1))
		(group

		# highlighted card
		(if (== i this.highlightCard)
			(group
			(dd_matrix_push)
			(this.cards[i].applyMatrix)
			(dd_scalef 1.15 1.15 1.15)
			(this.cards[i].drawRaw)
			(dd_translatef 0 0 0.01)
			(dd_scalef 0.055 0.055 0.055)
			(if this.labelConfirm
				(this.labelConfirm.draw)
			)
			(dd_matrix_pop)
			)
		# looking at card
		(&& (== i this.userLookingAtCard) (== this.highlightCard -1))
			(group
			(dd_matrix_push)
			(this.cards[i].applyMatrix)
			(dd_scalef 1.1 1.1 1.1)
			(this.cards[i].drawRaw)
			(dd_matrix_pop)
			)
		# else
			(this.cards[i].draw)
		)
		)
	)

	# draw selected card
	(if this.selectedCard2
		(this.selectedCard2.draw)
	)

	)
)

(class_function Player void drawUser ()
	(group

	)
)

(class_function Player void drawLookingAtText ()
	(group

#	(if (>= this.userLookingAtCard 0)
#		(group
#		(dd_matrix_push)
#		(dd_translatef 0 0 -5)
#		(this.cards[this.userLookingAtCard].drawText)
#		(dd_matrix_pop)
#		)
#	)

	)
)

(class_function Player void drawAI ()
	(group

	# draw player
	(dd_matrix_push)
	(dd_multMatrixf this)
	(this.mesh.draw)
	(dd_translatef 0 2.2 0.3)
	(dd_scalef 0.5 0.5 0.5)
	(this.label_int.drawInt this.cardsTotal)
	(dd_translatef 0 3 0)
	(this.label_name.draw)
	(dd_matrix_pop)

	)
)

(class_function Player void clean ()
	(group
	)
)

# add new card to the hand
(def ref dd_meshColour newCardMesh)
(def ref Card newCard)

(class_function Player void addCard (group Card card)
	(group

	# apply limit
	(if (>= this.cardsTotal 10)
		(echo "CANNOT GIVE MORE CARDS")
		(group

		(= this.cards[this.cardsTotal] card)
		(= this.cardsTotal (+ this.cardsTotal 1))

		(if this.isUser
			(this.calculateCardPositionsUser)
			(this.calculateCardPositions)
		)

		)
	)

	)
)

(class_function Player void removeCard (group int index)
	(group

	# no cards to remove
	(if (<= this.cardsTotal 0)
		(echo "NO CARDS TO REMOVE")
	# index out of bounds
	(|| (< index 0) (>= index this.cardsTotal))
		(echo "INDEX OUT OF BOUNDS TO REMOVE")
	# remove given card
		(group
			(def int temp)
			# shuffle all cards in the array down one step
			(for (def int i index) (< i (- this.cardsTotal 1)) (= i (+ i 1))
				(group
				(= temp (+ i 1))
				(= this.cards[i] this.cards[temp])
				)
			)
			(= temp (- this.cardsTotal 1))
			(= this.cards[temp] 0)

			# update new number of cards
			(= this.cardsTotal (- this.cardsTotal 1))

			# re-position cards
			(if this.isUser
				(this.calculateCardPositionsUser)
				(this.calculateCardPositions)
			)
		)
	)

	)
)

(class_function Player void calculateCardPositions ()
	(group
	(for (def int i 0) (< i this.cardsTotal) (= i (+ i 1))
		(group

		(def dd_matrix mat)
		(dd_matrix_identity mat)
		(dd_matrix_mult mat this)
		(dd_matrix_translate mat 0 2.5 0.2)
		(dd_matrix_translate mat (- (* i 0.42) (/ (* (- this.cardsTotal 1) 0.42) 2)) 0 0)
		(dd_matrix_rotate mat 180 0 1 0)
		(if (== this.cards[i].id CARD_ROSE)
			(dd_matrix_rotate mat 180 0 1 0)
		)
		(this.cards[i].setTarget mat)

		)
	)
	)
)

(class_function Player void calculateCardPositionsUser ()
	(group
	(for (def int i 0) (< i this.cardsTotal) (= i (+ i 1))
		(group

		(def dd_matrix mat)
		(dd_matrix_identity mat)
		(dd_matrix_mult mat this)
		(if AVDL_VR
			(group
			(dd_matrix_translate mat 0 3.2 0)
			(dd_matrix_rotate mat (- (* -25 i) (* (/ (- this.cardsTotal 1) 2.0) -25)) 0 1 0)
			)
			(group
			(dd_matrix_translate mat 0 3.7 0)
			(dd_matrix_rotate mat (- (* -30 i) (* (/ (- this.cardsTotal 1) 2.0) -30)) 0 1 0)
			)
		)
		(dd_matrix_rotate mat -43 1 0 0)
		(dd_matrix_translate mat 0 0 -1.2)
		(dd_matrix_rotate mat 25 1 0 0)
		(if AVDL_VR
			(dd_matrix_scale mat 0.7 0.7 0.7)
		)
		(this.cards[i].setTarget mat)
		)
	)
	)
)

(class_function Player void highlightLookingCard ()
	(group

	# player has cards
	(if (<= this.cardsTotal 0)
		(group
		(echo "cannot select card, no cards left")
		)
	# card is highlighted, either select it or remove highlight
	(>= this.highlightCard 0)
		(group

		# looking at highlighted card - select it
		(if (== this.highlightCard this.userLookingAtCard)
			(group

			(= this.selectedCard2 this.cards[this.highlightCard])

			(def dd_matrix mat)
			(dd_matrix_identity mat)
			(dd_matrix_mult mat this.actual)
			(dd_matrix_mult mat this.selectedMatrix)
			(this.selectedCard2.setTarget mat)

			# remove card from array
			(for (def int i (+ this.highlightCard 1)) (< i this.cardsTotal) (= i (+ i 1))
				(group
				(def int prevIndex (- i 1))
				(= this.cards[prevIndex] this.cards[i])
				)
			)

			(= this.cardsTotal (- this.cardsTotal 1))
			(if this.isUser
				(this.calculateCardPositionsUser)
				(this.calculateCardPositions)
			)

			)
		# looking elsewhere - de-highlight card
			(group
			(= this.highlightCard -1)
			(this.cardDeselectSound.play)
			)
		)
		)
	(&& (>= this.userLookingAtCard 0) (< this.userLookingAtCard this.cardsTotal))
		(group
		(= this.highlightCard this.userLookingAtCard)
		(this.cardSelectSound.play)
		)
	)

	)
)

(class_function Player void planCard (group int index)
	(group

	(if (<= this.cardsTotal 0)
		(group
		(echo "cannot select card, no cards left")
		)
	(&& (< index 0) (>= index this.cardsTotal))
		(group
		(echo "cannot select card, out of bounds")
		)
	# else
		(group
		(= this.selectedCard2 this.cards[index])
		(def dd_matrix mat)
		(dd_matrix_identity mat)
		(dd_matrix_mult mat this.actual)
		(dd_matrix_mult mat this.selectedMatrix)
		(this.selectedCard2.setTarget mat)

		# remove card from array
		(for (def int i (+ index 1)) (< i this.cardsTotal) (= i (+ i 1))
			(group
			(def int prevIndex (- i 1))
			(= this.cards[prevIndex] this.cards[i])
			)
		)

		(= this.cardsTotal (- this.cardsTotal 1))
		(if this.isUser
			(this.calculateCardPositionsUser)
			(this.calculateCardPositions)
		)

		(this.cardPlaceSound.play)

		) # selected card
	)

	)
)

(class_function Player void selectCard ()
	(group

	(if (<= this.cardsTotal 0)
		(group
		(echo "cannot select card, no cards left")
		)
	(&& (< this.userLookingAtCard 0) (>= this.userLookingAtCard this.cardsTotal))
		(group
		(echo "cannot select card, out of bounds")
		)
	# else
		(group

		# get index, either by card being looked at, or random for AI
		(def int index)
		(if (|| this.isUser this.isNetworkUser)
			(group
			(= index this.highlightCard)
			(= this.highlightCard -1)
			)
			(group
			(= index (dd_math_rand this.cardsTotal))
			)
		)
		(= this.selectedCard (dd_math_rand this.cardsTotal))
		(= this.selectedCard2 this.cards[index])
		(def dd_matrix mat)
		(dd_matrix_identity mat)
		(dd_matrix_mult mat this.actual)
		(dd_matrix_mult mat this.selectedMatrix)
		(this.selectedCard2.setTarget mat)

		# remove card from array
		(for (def int i (+ index 1)) (< i this.cardsTotal) (= i (+ i 1))
			(group
			(def int prevIndex (- i 1))
			(= this.cards[prevIndex] this.cards[i])
			)
		)

		(= this.cardsTotal (- this.cardsTotal 1))
		(if this.isUser
			(this.calculateCardPositionsUser)
			(this.calculateCardPositions)
		)

		) # selected card
	)

	)
)

(class_function Player void deselectCard ()
	(group
	(= this.selectedCard -1)
	(= this.selectedCard2 0)
	)
)

(class_function Player int hasPlannedCard ()
	(group
	(if (== this.selectedCard2 0)
		(return 0)
		(return 1)
	)
	)
)

(class_function Player void lookAtCard (group float rotX float rotY)
	(group

	(def int lookingAtCard -1)

	# looking at the hand's layer (Y-axis)
	(if (&& (< rotY -40) (> rotY -60))
		(group

		# looking at the hand - figure out which card
		(if (&& (> rotX (* (/ (- this.cardsTotal 0.0) 2.0) -30)) (< rotX (* (/ (- this.cardsTotal 0) 2.0) 30)))
			(group
			(= lookingAtCard (/ (+ (/ (* this.cardsTotal 30) 2.0) rotX) 30))
			)
		)

		)
	) # looking at hand's layer

	# looking at the same card as previously
	(if (== this.userLookingAtCard lookingAtCard)
		(group
		)
	# looking at a new card
		(group
		(this.cardLookSound.play)
		)
	)

	(= this.userLookingAtCard lookingAtCard)

	)
) # look at card

(class_function Player void lookAtCardByIndex (group int index)
	(group

	(if (|| (< index 0) (>= index this.cardsTotal))
		(return)
	)

	(def int lookingAtCard index)

	# looking at the same card as previously
	(if (== this.userLookingAtCard lookingAtCard)
		(group
		)
	# looking at a new card
		(group
		(this.cardLookSound.play)
		)
	)

	(= this.userLookingAtCard lookingAtCard)

	)
) # look at card

(class_function Player void lookAtCardPos (group dd_vec4 pos)
	(group

	(def float floatX)
	(def float floatY)
	(def float floatZ)
	(= floatX (dd_vec4_getX pos))
	(= floatY (dd_vec4_getY pos))
	(= floatZ (dd_vec4_getZ pos))

	(def int lookingAtCard -1)

	(if (== this.cardsTotal 0) (return))

	# looking at the hand's layer (Y-axis)
	(if (< floatY -0.5)
		(group

		# looking at the hand - figure out which card
		(if (&& (> floatX -0.6) (< floatX 0.6))
			(group
			(= lookingAtCard (/ (+ (/ (* this.cardsTotal 0.3) 2.0) floatX) 0.3))
			(= lookingAtCard (dd_math_min lookingAtCard this.cardsTotal))
			(= lookingAtCard (dd_math_max lookingAtCard 0))
			)
		)

		)
	) # looking at hand's layer

	# looking at the same card as previously
	(if (== this.userLookingAtCard lookingAtCard)
		(group
		)
	# looking at a new card
		(group
		#(this.cardLookSound.play)
		)
	)

	(= this.userLookingAtCard lookingAtCard)
	)
)

(class_function Player void setFont (group avdl_font font)
	(group
	(this.label_name.setFont font)
	(this.label_int.setFont font)
	)
)
