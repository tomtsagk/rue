(include "player.ddh")

(class_function Player void create (group)
	(group

	(= this.isUser 0)

	# player itself
	(this.mesh.load (asset "assets/character.asset" DD_PLY))
	(this.mesh.loadTexture (asset "assets/character_1.asset" DD_PLY))

	(this.cardMesh.load (asset "assets/card.asset" DD_PLY))
	(this.cardMesh.loadTexture (asset "assets/card_action_1_childish_king.asset" DD_PLY))

	# individual cards
	(= this.cardsTotal 0)

	# font
	(this.font.setAlign DD_STRING3D_ALIGN_CENTER)
	(this.font.setColorFront 1.0 1.0 1.0)
	(this.font.setColorBack 0.0 0.0 0.0)

	)
)

(class_function Player void draw (group)
	(group

	(if this.isUser
		(this.drawUser)
		(this.drawAI)
	)

	)
)

(class_function Player void drawUser (group)
	(group

	(dd_matrix_push)

	# translation for cards
	#(dd_translatef 0 3.5 0)
	#(dd_rotatef -30 1 0 0)
	#(dd_translatef 0 0 -1.2)
	#(dd_scalef 0.15 0.15 0.15)

	# draw cards
	(for (def int i 0) (< i this.cardsTotal) (= i (+ i 1))
		(group
		(dd_matrix_push)
		(this.cards2[i].draw)
		(dd_matrix_pop)
		)
	)

	(dd_matrix_pop)

	)
)

(class_function Player void drawAI (group)
	(group

	# draw player
	(dd_matrix_push)
	(dd_multMatrixf this)
	(this.mesh.draw)
	(dd_translatef 0 2.2 0.4)
	(dd_scalef 0.5 0.5 0.5)
	(this.font.drawInt this.cardsTotal)
	(dd_matrix_pop)

	# draw cards
	(for (def int i 0) (< i this.cardsTotal) (= i (+ i 1))
		(group
		(this.cards2[i].draw)
		)
	)

	)
)

(class_function Player void clean (group)
	(group
	)
)

# add new card to the hand
(def ref dd_meshColour newCardMesh)
(def ref Card newCard)

(class_function Player void addCard (group Card card)
	(group

	# apply limit
	(if (>= this.cardsTotal 10)
		(echo "CANNOT GIVE MORE CARDS")
	(group
	(= this.cards2[this.cardsTotal] card)
	(= this.cardsTotal (+ this.cardsTotal 1))

	(if this.isUser
		(this.calculateCardPositionsUser)
		(this.calculateCardPositions)
	)
	)
	)

	)
)

(class_function Player void calculateCardPositions (group)
	(group
	(for (def int i 0) (< i this.cardsTotal) (= i (+ i 1))
		(group
		(dd_matrix_identity this.cards2[i].target)
		(dd_matrix_mult this.cards2[i].target this)
		(dd_matrix_translate this.cards2[i].target 0 2.5 0.3)
		(dd_matrix_scale this.cards2[i].target 0.15 0.15 0.15)
		(dd_matrix_translate this.cards2[i].target (- (* i 2.7) (/ (* (- this.cardsTotal 1) 2.7) 2)) 0 0)
		(dd_matrix_rotate this.cards2[i].target 180 0 1 0)
		)
	)
	)
)

(class_function Player void calculateCardPositionsUser (group)
	(group
	(for (def int i 0) (< i this.cardsTotal) (= i (+ i 1))
		(group
		(dd_matrix_identity this.cards2[i].target)
		(dd_matrix_mult this.cards2[i].target this)
		(dd_matrix_translate this.cards2[i].target 0 3.7 0)
		(dd_matrix_rotate this.cards2[i].target (- (* -25 i) (* (/ (- this.cardsTotal 1) 2.0) -25)) 0 1 0)
		(dd_matrix_rotate this.cards2[i].target -35 1 0 0)
		(dd_matrix_translate this.cards2[i].target 0 0 -1.2)
		(dd_matrix_rotate this.cards2[i].target 15 1 0 0)
		(dd_matrix_scale this.cards2[i].target 0.15 0.15 0.15)
		)
	)
	)
)
