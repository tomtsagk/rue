(include "menu.ddh")
(include "game.ddh")
(include "credits.ddh")

(function float particleMovementY (group float t)
	(group
	(return (* (dd_math_sin (dd_math_dec2rad (* t 360.0 2))) 1.0))
	)
)

(function float particleMovementZ (group float t)
	(group
	(return (* t 5))
	)
)

(function float particleScale (group float t)
	(group
	(if (< t 0.5)
		(return (* (dd_math_sin (dd_math_dec2rad (* t 90.0))) 2.0))
		(return (* (dd_math_sin (dd_math_dec2rad (* (- 1.0 t) 90.0))) 2.0))
	)
	)
)

(class_function world_menu void create ()
	(group

	# background
	(= this.bg_rotation 0)
	(this.bg.load (asset "assets/stage_2.asset" DD_PLY))

	# background particles
	(this.bgParticleMesh.load (asset "assets/particle.asset" DD_PLY))
	(this.bgParticleSystem.assignAsset this.bgParticleMesh)
	(this.bgParticleSystem.setParticleLife 1000)
	(this.bgParticleSystem.setDelay 30)
	(this.bgParticleSystem.setParticlePosition 0 4 -5)
	(this.bgParticleSystem.setParticlePositionFuzz 16 4 11)
	(this.bgParticleSystem.setParticlePositionYFunc particleMovementY)
	(this.bgParticleSystem.setParticlePositionZFunc particleMovementZ)
	(this.bgParticleSystem.setParticleScaleFunc particleScale)
	(this.bgParticleSystem.setParticlesTotal 20)

	(for (def int i 0) (< i 100) (= i (+ i 1))
		(this.bgParticleSystem.update)
	)

	# eyes
	(this.lookatMesh.load (asset "assets/eye.asset" DD_PLY))
	(= this.eyeTargetX 0)
	(= this.eyeTargetY 0)

	(this.menuHome.enable)

	)
)

(class_function world_menu void onload ()
	(group
	(this.theme.load (asset "assets/main_menu_theme.asset" DD_PLY) 0)
	(this.theme.playLoop -1)
	)
)

(class_function world_menu void resize ()
	(group
	(this.menuCustomGame.resize)
	(this.menuHome.resize)
	(this.menuStages.resize)
	(this.opt.resize)
	)
)

(class_function world_menu void update ()
	(group

	# constant background rotation
	(= this.bg_rotation (+ this.bg_rotation 0.01))
	(if (>= this.bg_rotation 360)
		(= this.bg_rotation (- this.bg_rotation 360))
	)

	(this.bgParticleSystem.update)

	(this.menuCustomGame.update)
	(this.menuHome.update)
	(this.menuStages.update)
	(this.opt.update)

	# lookat animation
	(if (this.opt.isEnabled)
		(group
		(= this.eyeTargetX (+ this.eyeTargetX (* (- this.opt.button[this.opt.selection].x this.eyeTargetX) 0.1)))
		(= this.eyeTargetY (+ this.eyeTargetY (* (- this.opt.button[this.opt.selection].y this.eyeTargetY) 0.1)))

		(= this.eyeTargetX (+ this.eyeTargetX (* this.opt.button[this.opt.selection].idleRot -0.003)))
		)
	# lookat home menu buttons
	(this.menuHome.isEnabled)
		(group
		(= this.eyeTargetX (+ this.eyeTargetX (* (- this.menuHome.button[this.menuHome.selection].x this.eyeTargetX) 0.1)))
		(= this.eyeTargetY (+ this.eyeTargetY (* (- this.menuHome.button[this.menuHome.selection].y this.eyeTargetY) 0.1)))

		(= this.eyeTargetX (+ this.eyeTargetX (* this.menuHome.button[this.menuHome.selection].idleRot -0.003)))
		)
	# lookat custom game menu buttons
	(this.menuCustomGame.isEnabled)
		(group
		(= this.eyeTargetX (+ this.eyeTargetX (* (- this.menuCustomGame.button[this.menuCustomGame.selection].x this.eyeTargetX) 0.1)))
		(= this.eyeTargetY (+ this.eyeTargetY (* (- this.menuCustomGame.button[this.menuCustomGame.selection].y this.eyeTargetY) 0.1)))

		(= this.eyeTargetX (+ this.eyeTargetX (* this.menuCustomGame.button[this.menuCustomGame.selection].idleRot -0.003)))
		)
	# lookat stages menu buttons
	(this.menuStages.isEnabled)
		(group
		(= this.eyeTargetX (+ this.eyeTargetX (* (- this.menuStages.button[this.menuStages.selection].x this.eyeTargetX) 0.1)))
		(= this.eyeTargetY (+ this.eyeTargetY (* (- this.menuStages.button[this.menuStages.selection].y this.eyeTargetY) 0.1)))

		(= this.eyeTargetX (+ this.eyeTargetX (* this.menuStages.button[this.menuStages.selection].idleRot -0.003)))
		)
	)
	(def float tempZ 3)

	# lookat
	(dd_matrix_identity this.matrixEyeL)
	(dd_matrix_lookat this.matrixEyeL (+ this.eyeTargetX 1.3) this.eyeTargetY tempZ)

	(dd_matrix_identity this.matrixEyeR)
	(dd_matrix_lookat this.matrixEyeR (+ this.eyeTargetX -1.3) this.eyeTargetY tempZ)

	)
) # update

(class_function world_menu void draw ()
	(group

	# draw background
	(dd_matrix_push)
	(dd_translatef 0 -3 -8)
	(dd_rotatef (+ 45 this.bg_rotation) 0 1 0)
	(dd_rotatef 15 -1 0 0)
	(this.bg.draw)
	(this.bgParticleSystem.draw)
	(dd_matrix_pop)

	# clear depth
	(avdl_clear_depth)

	# draw eyes
	(dd_matrix_push)
	(dd_translatef -1.3 0.5 -8)
	(dd_multMatrixf this.matrixEyeL)
	(this.lookatMesh.draw)
	(dd_matrix_pop)

	(dd_matrix_push)
	(dd_translatef 1.3 0.5 -8)
	(dd_multMatrixf this.matrixEyeR)
	(this.lookatMesh.draw)
	(dd_matrix_pop)

	# clear depth
	(avdl_clear_depth)

	(this.menuCustomGame.draw)
	(this.menuHome.draw)
	(this.menuStages.draw)
	(this.opt.draw)

	)
)

(class_function world_menu void key_input (group char key)
	(group
	(if (== key 27)
		(= dd_flag_exit 1) # exit game
	) # key input
	)
)

(class_function world_menu void clean ()
	(group
	(this.theme.stop)
	)
)

(class_function world_menu void mouse_input (group int button int type)
	(group

	# options menu is open
	(if (this.opt.isEnabled)
		(group
		(if (this.opt.mouse_input button type)
			(group
			(this.menuHome.enable)
			)
		)
		(return)
		)
	# home menu is open
	(this.menuHome.isEnabled)
		(group
		(def int returnMenu (this.menuHome.mouse_input button type))
		# custom game
		(if (== returnMenu 1)
			(group
			(this.menuCustomGame.enable)
			)
		# play
		(== returnMenu 2)
			(group
			(this.menuStages.enable)
			)
		# options
		(== returnMenu 3)
			(group
			(this.opt.enable)
			)
		# credits
		(== returnMenu 4)
			(group
			(dd_world_prepare world_credits 1.0)
			(dd_world_ready)
			)
		)
		)
	# custom game menu is open
	(this.menuCustomGame.isEnabled)
		(group
		(def int returnMenu (this.menuCustomGame.mouse_input button type))
		# back to home
		(if (== returnMenu 1)
			(group
			(this.menuHome.enable)
			)
		# start game
		(== returnMenu 2)
			(group
			(dd_world_prepare world_game 1.0)
			(dd_world_ready)
			)
		)

		)
	# stages menu is open
	(this.menuStages.isEnabled)
		(group
		(def int returnMenu (this.menuStages.mouse_input button type))
		# back to home
		(if (== returnMenu -1)
			(group
			(this.menuHome.enable)
			)
		(== returnMenu 1)
			(group
			(dd_world_prepare world_game 1.0)
			(dd_world_ready)
			)
		)

		)
	)

	)
) # mouse input
