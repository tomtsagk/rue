(include "options.ddh")

(class_function options void create ()
	(group

	# font
	(this.font.setAlign DD_STRING3D_ALIGN_CENTER)
	#(this.font.setColorFront 0.8 0.8 0.8)
	#(this.font.setColorBack 0.3 0.3 0.3)

	# buttons
	(= this.buttonTotal 4)
	(this.button[0].mesh.load (asset "assets/button.ply" DD_PLY))
	(this.button[0].label.setText "Music Playing")
	(this.button[0].description.setText "Select to mute/unmute music")

	(this.button[1].mesh.load (asset "assets/button.ply" DD_PLY))
	(this.button[1].label.setText "Audio Playing")
	(this.button[1].description.setText "Select to mute/unmute audio")

	# toggle fullscreen button
	(this.button[2].mesh.load (asset "assets/button.ply" DD_PLY))
	(this.button[2].label.setText "Toggle Fullscreen")
	(this.button[2].description.setText "Swap window and fullscreen")
	(if (== (dd_canFullscreenToggle) 0)
		(= this.button[2].isVisible 0)
	)

	# back button
	(this.button[3].mesh.load (asset "assets/button.ply" DD_PLY))
	(this.button[3].label.setText "Back")
	(this.button[3].description.setText "Return to main menu")

	# Yes/No labels
	(this.labelYes.setText "Yes")
	(this.labelYes.setAlign DD_STRING3D_ALIGN_CENTER)
	(this.labelNo.setText "No")
	(this.labelNo.setAlign DD_STRING3D_ALIGN_CENTER)

	# text display
	(this.textButton.setSize 1 1)
	(this.textButton.setProportionalPosition 0.5 0.15)
	(this.textButton.setProportionalWidth 0.2)

	(= this.selection 0)
	(= this.selectionClick -1)
	#(= this.isMovingToNewWorld 0)

	(this.buttonProgram.setVertexShader
		(multistring
			"AVDL_IN vec3 colour;\n"
			"AVDL_IN vec2 texCoord;\n"

			"uniform vec3 tint;\n"

			"AVDL_OUT vec2 outTexCoord;\n"
			"AVDL_OUT vec4 outColour;\n"

			"void main() {\n"
			"	vec4 pos = final_position();\n"
			"       gl_Position = pos;\n"
			"       outColour = vec4(colour.rgb *tint.rgb, 1.0);\n"
			"	outTexCoord  = texCoord;\n"
			"}\n"
		)
	)
	(this.buttonProgram.setFragmentShader
		(multistring
			"AVDL_IN vec4 outColour;\n"
			"AVDL_IN vec2 outTexCoord;\n"

			"uniform sampler2D image;\n"

			"void main() {\n"
			"	avdl_frag_color = outColour +avdl_texture(image, outTexCoord);\n"

			# For some reason the following line makes the shader to not compile on the following system:
			#  GeForce GTX 960/PCIe/SSE2
			#  GL version: 4.6.0 NVIDIA 456.71
			#  glsl version: 4.60 NVIDIA
			#"	avdl_frag_color = outColour;\n"
			"}\n"
		)
	)

	(= this.buttonClickCol[0] 0.389)
	(= this.buttonClickCol[1] 0.055)
	(= this.buttonClickCol[2] 0.682)

	(= this.buttonActiveCol[0] 0.916)
	(= this.buttonActiveCol[1] 0.516)
	(= this.buttonActiveCol[2] 0.074)

	(= this.buttonInactiveCol[0] 0.694)
	(= this.buttonInactiveCol[1] 0.137)
	(= this.buttonInactiveCol[2] 0.137)

	(= this.enabled 0)
	(= this.animationOpen 0)

	(this.hoverSound.load (asset "assets/button_hover.ogg" DD_PLY))
	(this.clickSmallSound.load (asset "assets/button_click_small.ogg" DD_PLY))
	(this.clickBigSound.load (asset "assets/button_click_big.ogg" DD_PLY))

	(= this.volumeMusic (avdl_music_getVolume))
	(= this.volumeSound (avdl_sound_getVolume))
	(this.volumeParticle.load (asset "assets/ui_particle.ply" DD_PLY))
	(this.volumeParticleStart.load (asset "assets/ui_particle_start.ply" DD_PLY))

	)
)

(class_function options void resize ()
	(group
	# portrait
	(if (&& (== AVDL_VR 0) (< (dd_window_width) (dd_window_height)))
		(group
		# music toggle
		(this.button[0].setSize 1 1)
		(this.button[0].setProportionalPosition 0.5 0.7)
		(this.button[0].setProportionalWidth 0.4)
		(this.button[0].setProportionalSize 0.6)

		# audio toggle
		(this.button[1].setSize 1 1)
		(this.button[1].setProportionalPosition 0.5 0.5)
		(this.button[1].setProportionalWidth 0.4)
		(this.button[1].setProportionalSize 0.6)

		# full screen toggle (optional)
		(this.button[2].setSize 1 1)
		(this.button[2].setProportionalPosition 0.5 0.3)
		(this.button[2].setProportionalWidth 0.4)
		(this.button[2].setProportionalSize 0.6)

		# back button
		(this.button[3].setSize 1 1)
		(this.button[3].setProportionalPosition 0.15 0.85)
		(this.button[3].setProportionalWidth 0.4)
		(this.button[3].setProportionalSize 0.6)
		)
	# landscape
		(group
		# music toggle
		(this.button[0].setSize 1 1)
		(this.button[0].setProportionalPosition 0.3 0.58)
		(this.button[0].setProportionalWidth 0.3)
		(this.button[0].setProportionalSize 0.7)

		# audio toggle
		(this.button[1].setSize 1 1)
		(this.button[1].setProportionalPosition 0.7 0.58)
		(this.button[1].setProportionalWidth 0.3)
		(this.button[1].setProportionalSize 0.7)

		# full screen toggle (optional)
		(this.button[2].setSize 1 1)
		(this.button[2].setProportionalPosition 0.5 0.32)
		(this.button[2].setProportionalWidth 0.3)
		(this.button[2].setProportionalSize 0.7)

		# back button
		(this.button[3].setSize 1 1)
		(this.button[3].setProportionalPosition 0.15 0.85)
		(this.button[3].setProportionalWidth 0.2)
		(this.button[3].setProportionalSize 1.0)
		)
	)

	(for (def int i 0) (< i this.buttonTotal) (= i (+ i 1))
		(this.button[i].resize)
	)
	(this.textButton.resize)
	)
)

(class_function options void update (group float dt)
	(group

	# disabled and animated out, do not update
	(if (&& (== (this.isEnabled) 0) (< this.animationOpen 0.01)) (return))

	# animate opening/closing
	(if (this.isEnabled)
		#(= this.animationOpen (dd_math_ease_linear 0.05 this.animationOpen 1))
		(= this.animationOpen (dd_math_min (+ this.animationOpen 0.08) 1))
		(= this.animationOpen (dd_math_max (- this.animationOpen 0.08) 0))
		#(= this.animationOpen (dd_math_ease_linear 0.05 this.animationOpen 0))
	)

	# disabled do not calculate hovering buttons
	(if (== (this.isEnabled) 0) (return))

	(if (== AVDL_VR 0)
		(group
		# check if mouse is hovering over a button
		(for (def int i 0) (< i this.buttonTotal) (= i (+ i 1))
			(group

			(if (== this.selection i)
				(group
				)
				(if (this.button[i].hasMouseCollided)
					(group
					(this.hoverSound.play)
					(= this.selection i)
					)
				)
			)

			(this.button[i].update dt (== this.selection i) (== this.selectionClick i))

			)
		)
		)
	)

	# on systems with visible controllers (quest 2 for now)
	(if (dd_matrix_hasVisibleControllers)
		(for (def int i 1) (< i 2) (= i (+ i 1))
			(group
			(if (dd_matrix_isControllerVisible i)
				(group
				# check if mouse is hovering over a button
				(for (def int j 0) (< j this.buttonTotal) (= j (+ j 1))
					(group

					(if (== this.selection j)
						(group
						)
						(if (this.button[j].hasPositionCollided (dd_matrix_getControllerPosition i))
							(group
							(this.hoverSound.play)
							(= this.selection j)
							)
						)
					)

					(this.button[j].update dt (== this.selection j) (== this.selectionClick j))

					)
				)
				)
			)
			)
		)
	)

	)
) # update

(class_function options void draw ()
	(group

	# do not draw if animated out
	(if (< this.animationOpen 0.01) (return))

	# draw buttons
	(this.buttonProgram.useProgram)
	(def int colLoc)
	(= colLoc (avdl_getUniformLocation this.buttonProgram "tint"))
	(for (def int i 0) (< i this.buttonTotal) (= i (+ i 1))
		(group
		(dd_matrix_push)

		# change the shader colour when clicked
		(if (> this.button[i].idleCol 1.0)
			(avdl_graphics_SetUniform3f colLoc
				(dd_math_ease_linear
					(- this.button[i].idleCol 1.0)
					this.buttonActiveCol[0] this.buttonClickCol[0]
				)
				(dd_math_ease_linear
					(- this.button[i].idleCol 1.0)
					this.buttonActiveCol[1] this.buttonClickCol[1]
				)
				(dd_math_ease_linear
					(- this.button[i].idleCol 1.0)
					this.buttonActiveCol[2] this.buttonClickCol[2]
				)
			)
		# change colour when selected
			(avdl_graphics_SetUniform3f colLoc
				(dd_math_ease_linear this.button[i].idleCol this.buttonInactiveCol[0] this.buttonActiveCol[0])
				(dd_math_ease_linear this.button[i].idleCol this.buttonInactiveCol[1] this.buttonActiveCol[1])
				(dd_math_ease_linear this.button[i].idleCol this.buttonInactiveCol[2] this.buttonActiveCol[2])
			)
		)

		(this.button[i].applyTransform)
		(this.button[i].applyMatrixTransform)
		(def float anim)
		# big numbers make animation faster, but blends less together (max 4 - number of buttons)
		# small numbers make animation slower. At 1.0, all buttons animate together
		(def float animationSpeed 2.0)
		(def float animationSpeedMult (- 1.0 (/ 1.0 animationSpeed)))
		(= anim (dd_math_min
				(dd_math_max
					(* (- this.animationOpen (* (/ animationSpeedMult (- this.buttonTotal 1)) i)) animationSpeed)
					0
				)
				1
			)
		)
		(dd_scalef anim anim anim)
		(this.button[i].drawRaw)

		(dd_translatef 0 0 0.1)
		(dd_scalef 0.3 0.3 0.3)

		(if this.button[i].isVisible
			(this.button[i].label.draw)
		)

		(dd_matrix_pop)
		)
	)
	(avdl_useProgram 0)

	# draw button description
	(if (>= this.selection 0)
		(group
		(dd_matrix_push)
		(this.textButton.applyTransform)
		(this.textButton.applyMatrixTransform)
		(dd_translatef 0 0 0.1)
		(dd_scalef 0.3 0.3 0.3)
		#(def float limit (* (dd_screen_width_get this.textButton.z) 1.8))
		#(this.button[this.selection].description.draw)
		(this.button[this.selection].description.drawLimit 25)
		(dd_matrix_pop)
		)
	)

	# draw music label
	(dd_matrix_push)
	(this.button[0].applyTransform)
	(this.button[0].applyMatrixTransform)
	(dd_translatef 0 -0.4 0.1)
	(dd_scalef 0.4 0.4 0.4)
	(if (> this.volumeMusic 2)
		(this.labelYes.draw)
		(this.labelNo.draw)
	)
	(dd_matrix_pop)

	# draw audio label
	(dd_matrix_push)
	(this.button[1].applyTransform)
	(this.button[1].applyMatrixTransform)
	(dd_translatef 0 -0.4 0.1)
	(dd_scalef 0.4 0.4 0.4)
	(if (> this.volumeSound 2)
		(this.labelYes.draw)
		(this.labelNo.draw)
	)

	(dd_matrix_pop)

	)
)

(class_function options int key_input (group int key)
	(group

	# enter or space to confirm
	(if (|| (== key 13) (== key 32) (== key AVDL_INPUT_QUEST2_TRIGGER_R))
		(group
		(return (this.button_selection this.selection))
		)
	)

	# navigation - portrait
	(if (< (dd_window_width) (dd_window_height))
		(group

		# left arrow or 'a'
		(if (|| (== key 1) (== key 97))
			(group

			# * -> back
			(= this.selection 3)

			)
		# right arrow or 'd'
		(|| (== key 3) (== key 100))
			(group

			# back -> music
			(if (== this.selection 3)
				(= this.selection 0)
			)

			)
		# up arrow or 'w'
		(|| (== key 2) (== key 119))
			(group

			# toggle -> audio
			(if (== this.selection 2)
				(= this.selection 1)
			# audio -> music
			(== this.selection 1)
				(= this.selection 0)
			# music -> back
			(== this.selection 0)
				(= this.selection 3)
			)

			)
		# down arrow or 's'
		(|| (== key 4) (== key 115))
			(group

			# audio -> toggle
			(if (== this.selection 1)
				(= this.selection 2)
			# music -> audio
			(== this.selection 0)
				(= this.selection 1)
			# back -> music
			(== this.selection 3)
				(= this.selection 0)
			)

			)
		)

		)
	# navigation - landscape
		(group

		# left arrow or 'a'
		(if (|| (== key 1) (== key 97))
			(group

			# audio -> music
			(if (== this.selection 1)
				(= this.selection 0)
			# toggle -> music
			(== this.selection 2)
				(= this.selection 0)
			# music -> back
			(== this.selection 0)
				(= this.selection 3)
			)

			)
		# right arrow or 'd'
		(|| (== key 3) (== key 100))
			(group

			# back -> music
			(if (== this.selection 3)
				(= this.selection 0)
			# music -> audio
			(== this.selection 0)
				(= this.selection 1)
			# toggle -> audio
			(== this.selection 2)
				(= this.selection 1)
			)

			)
		# up arrow or 'w'
		(|| (== key 2) (== key 119))
			(group

			# toggle -> music
			(if (== this.selection 2)
				(= this.selection 0)
			# music -> back
			(== this.selection 0)
				(= this.selection 3)
			# audio -> back
			(== this.selection 1)
				(= this.selection 3)
			)

			)
		# down arrow or 's'
		(|| (== key 4) (== key 115))
			(group

			# back -> music
			(if (== this.selection 3)
				(= this.selection 0)
			# music -> toggle
			(== this.selection 0)
				(= this.selection 2)
			# audio -> toggle
			(== this.selection 1)
				(= this.selection 2)
			)

			)
		)

		)
	)

	(return 0)

	)
)

(class_function options void clean ()
	(group
	)
)

(class_function options int mouse_input (group int button int type)
	(group

	# re-check selection, this allows the user to directly click on a button
	# in a touch screen device
	(for (def int i 0) (< i this.buttonTotal) (= i (+ i 1))
		(group

		(if (this.button[i].hasMouseCollided)
			(group
			(= this.selection i)
			)
		)

		)
	)

	# control the camera look at point when click and dragging
	(if (== type DD_INPUT_MOUSE_TYPE_PRESSED)
		(group
		(if (&& (>= this.selection 0) (this.button[this.selection].hasMouseCollided))
			(= this.selectionClick this.selection)
		)
		)
	(== type DD_INPUT_MOUSE_TYPE_RELEASED)
		(group

		(def int selectionClickTemp this.selectionClick)
		(= this.selectionClick -1)

		# clicked on a button
		(if (&& (== this.selection selectionClickTemp)
			(>= this.selection 0) (this.button[this.selection].hasMouseCollided))
			(group

			(return (this.button_selection selectionClickTemp))

			)
		)

		)
	) # mouse button released

	(return 0)

	)
) # mouse input

(class_function options int isEnabled ()
	(group
	(return this.enabled)
	)
)

(class_function options void enable ()
	(group
	(= this.enabled 1)
	(= this.selectionClick -1)
	)
)

(class_function options void disable ()
	(group
	(= this.enabled 0)
	)
)

(class_function options int button_selection (int button_index)
	(group
	(if (== button_index 0)
		(group
		(this.clickSmallSound.play)
		(if (< this.volumeMusic 10)
			(= this.volumeMusic 100)
			(= this.volumeMusic 0)
		)
		(avdl_music_setVolume this.volumeMusic)
		(if (< this.volumeMusic 10)
			(return 2)
			(return 3)
		)
		)
	(== button_index 1)
		(group
		(this.clickSmallSound.play)
		(if (< this.volumeSound 10)
			(= this.volumeSound 100)
			(= this.volumeSound 0)
		)
		(avdl_sound_setVolume this.volumeSound)
		(if (< this.volumeSound 10)
			(return 4)
			(return 5)
		)
		)
	(== button_index 2)
		(group
		(this.clickSmallSound.play)
		(dd_fullscreenToggle)
		)
	(== button_index 3)
		(group
		(this.clickBigSound.play)
		(this.disable)
		(return 1)
		)
	)

	(return 0)
	)
)
