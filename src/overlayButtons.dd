(include "overlayButtons.ddh")

(class_function OverlayButtons void create ()
	(group

	(dd_matrix_identity this.previousLoc)
	(= this.cardToShow 0)
	(= this.isLookingAtCard 0)
	(= this.hasConfirmed 0)

	(this.bgMesh.load (asset "assets/overlay_background.ply" DD_PLY))
	(= this.bg_rotation 0)

	(= this.waitUntilCardConfirmed 1)

	(this.confirmLabel.setAlign DD_STRING3D_ALIGN_CENTER)
	(this.confirmLabel.setText "Confirm ?")
	(this.skipLabel.setAlign DD_STRING3D_ALIGN_CENTER)
	(this.skipLabel.setText "Skip effect ?")

	(if AVDL_VR
		(group
		(= this.cardX 0)
		(= this.cardY 3.4)
		(= this.cardZ 3.5)
		)
		(group
		(= this.cardX 0)
		(= this.cardY 4.2)
		(= this.cardZ 3)
		)
	)

	)
)

(class_function OverlayButtons void update ()
	(group
	(= this.bg_rotation (+ this.bg_rotation 0.3))
	(if this.cardToShow
		(group
		(def float ovCount)
		(= ovCount this.cardToShow.counter)
		)
	)
	)
)

# draw the card with its transformation matrix
(class_function OverlayButtons void draw ()
	(group
	(if this.cardToShow
		(group
		(dd_matrix_push)
		(this.cardToShow.applyMatrix)
		(if this.hasConfirmed
			(group
			(dd_matrix_push)
			(dd_translatef 0 0 0.01)
			(dd_scalef 0.1 0.1 0.1)
			(if this.waitUntilCardConfirmed
				(this.confirmLabel.draw)
				(this.skipLabel.draw)
			)
			(dd_matrix_pop)
			)
		)
		(dd_translatef 0 0 -0.1)
		(dd_rotatef this.bg_rotation 0 0 1)
		(this.bgMesh.draw)
		(dd_matrix_pop)
		)
	)
	)
)

(class_function OverlayButtons void clean ()
	(group
	)
)

(class_function OverlayButtons void lookAt (group int x int y)
	(group

	(if (&& (> x -6) (< x 6)
	        (> y -3) (< y 14)
		)
		(group
		(if (== this.isLookingAtCard 0)
			(group
			(= this.isLookingAtCard 1)
			(def dd_matrix mat)
			(dd_matrix_identity mat)
			(dd_matrix_translate mat this.cardX this.cardY this.cardZ)
			(if (== AVDL_VR 0)
				(dd_matrix_scale mat 1.1 1.1 1.1)
				(dd_matrix_scale mat 0.4 0.4 0.4)
			)
			(this.cardToShow.setTarget mat)
			)
		)
		)
		(group
		(if this.isLookingAtCard
			(group
			(= this.isLookingAtCard 0)
			(def dd_matrix mat)
			(dd_matrix_identity mat)
			(dd_matrix_translate mat this.cardX this.cardY this.cardZ)
			(if AVDL_VR
				(dd_matrix_scale mat 0.3 0.3 0.3)
			)
			(this.cardToShow.setTarget mat)
			)
		)
		)
	)

	)
)

(class_function OverlayButtons int click ()
	(group
	# looking at the card shown
	(if this.isLookingAtCard
		(group
		# confirmed choice - select it
		(if this.hasConfirmed
			(group
			(this.unsetCard)
			(return 1)
			)
		# unconfirmed choice - confirm it
			(group
			(= this.hasConfirmed 1)
			(return 0)
			)
		)
		)
	# looking somewhere else
		(group
		(if this.hasConfirmed
			(= this.hasConfirmed 0)
		)
		)
	)
	(return 0)
	)
)

(class_function OverlayButtons void unsetCard ()
	(group
	(if this.cardToShow
		(group
		(= this.isLookingAtCard 0)
		(this.cardToShow.setTarget this.previousLoc)
		(= this.cardToShow 0)
		)
	)
	)
)

(class_function OverlayButtons void setCard (group Card c)
	(group
	(= this.isLookingAtCard 0)
	(def dd_matrix mat)
	(dd_matrix_copy this.previousLoc c.target)
	(dd_matrix_identity mat)
	(dd_matrix_translate mat this.cardX this.cardY this.cardZ)
	(if AVDL_VR
		(dd_matrix_scale mat 0.3 0.3 0.3)
	)
	(c.setTarget c mat)
	(= this.cardToShow c)
	(= this.hasConfirmed 0)
	)
)

(class_function OverlayButtons void lookAtPhysical (group dd_vec4 pos)
	(group

	(if (== this.cardToShow 0)
		(return)
	)

	(if (&& (< (dd_math_abs (dd_vec4_getX pos)) 0.1)
		(< (dd_math_abs (- (dd_vec4_getY pos) 0.2)) 0.1)
		(< (dd_math_abs (+ (dd_vec4_getZ pos) 0.5)) 0.1)
		)
		(group
		(if (== this.isLookingAtCard 0)
			(group
			(= this.isLookingAtCard 1)
			(def dd_matrix mat)
			(dd_matrix_identity mat)
			(dd_matrix_translate mat this.cardX this.cardY this.cardZ)
			(dd_matrix_scale mat 0.4 0.4 0.4)
			(this.cardToShow.setTarget mat)
			)
		)
		)
		(group
		(if this.isLookingAtCard
			(group
			(= this.isLookingAtCard 0)
			(def dd_matrix mat)
			(dd_matrix_identity mat)
			(dd_matrix_translate mat this.cardX this.cardY this.cardZ)
			(dd_matrix_scale mat 0.3 0.3 0.3)
			(this.cardToShow.setTarget mat)
			)
		)
		)
	)
	)
)

(class_function OverlayButtons void setFont (group avdl_font font)
	(group
	(this.confirmLabel.setFont font)
	(this.skipLabel.setFont font)
	)
)
